# 0x01 Getting Started

Create Database for javaï¼š

> codeql database create test4fun --language="java" --command="mvn clean install --file pom.xml" --source-root=D:\Code\Java\CodeQL\Demo\test1

Get Familiar With This Weird And Wicked Declarative ProgramğŸ¤§

Here is an exampleï¼šFinding if statements which have an empty then block

```sql
import java

from IfStmt ifStmt, BlockStmt block
where
  block = ifStmt.getThen() and
  block.getNumStmt() = 0
select ifStmt, "This isf-statement has an empty then-block"
```

* Predicate Wrap

```sql
import java

predicate isEmpty(BlockStmt block) {
  block.getNumStmt() = 0
}

from IfStmt ifStmt
where
  isEmpty(ifStmt.getThen())
select ifStmt
```

* Class Wrap

```sql
import java

class EmptyBlock extends BlockStmt{
  EmptyBlock(){
    this.getNumStmt() = 0
  }
}
from IfStmt ifStmt
where
  ifStmt.getThen() instanceof EmptyBlock
select ifStmt
```

Or Use It In This Way

```sql
import java

class EmptyBlock extends BlockStmt{
  EmptyBlock(){
    this.getNumStmt() = 0
  }
}
from IfStmt ifStmt, EmptyBlock block
where
  ifStmt.getThen() = block
select ifStmt
```

# 0x02 Apprentice Lab

`Unsafe deserialization in Apache Struts â€”â€” CVE-2017-9805`

Download and unzipğŸ‘‰ [apache_struts_cve_2017_9805.zip database](https://github.com/githubsatelliteworkshops/codeql/releases/download/v1.0/apache_struts_cve_2017_9805.zip)

## Finding XML deserialization

XStream is a Java framework for serializing Java objects to XML used by Apache Struts. It provides a method `XStream.fromXML` for deserializing XML to a Java object. By default, the input is not validated in any way, and is vulnerable to remote code execution exploits. In this section, we will identify calls to `fromXML` in the codebase.

1. æŸ¥æ‰¾ç¨‹åºä¸­çš„æ‰€æœ‰æ–¹æ³•è°ƒç”¨

```sql
import java

from MethodAccess call
select call
```

2. æ‰¾åˆ°æ‰€æœ‰æ–¹æ³•è°ƒç”¨åŠå…¶å¯¹åº”çš„æ–¹æ³•å£°æ˜

```sql
import java

from MethodAccess call, Method method
where call.getMethod() = method
select call, method
```

3. æ‰¾åˆ°ç¨‹åºä¸­æ‰€æœ‰è°ƒç”¨`fromXML`çš„åœ°æ–¹

```sql
import java

from MethodAccess call, Method method
where 
  call.getMethod() = method and
  method.getName() = "fromXML"
select call, method
```

å¯ä»¥ç®€åŒ–ä¸º

```sql
import java

from MethodAccess fromXML
where 
  fromXML.getMethod().getName() = "fromXML"
select fromXML
```

4. æ‰¾å‡ºè°ƒç”¨`fromXML`æ–¹æ³•çš„ç¬¬ä¸€ä¸ªå‚æ•°

```sql
import java

from MethodAccess fromXML, Expr arg
where 
  fromXML.getMethod().getName() = "fromXML" and
  arg = fromXML.getArgument(0)
select fromXML, arg
```

5. ä½¿ç”¨è°“è¯åŒ…è£…

```sql
import java

predicate isXMLDeserialized(Expr arg) {
  exists(MethodAccess fromXML | 
    fromXML.getMethod().getName() = "fromXML" and
    arg = fromXML.getArgument(0)  
  )
}

from Expr arg
where isXMLDeserialized(arg)
select arg
```

## Find the implementations of the toObject method from ContentTypeHandler

1. åˆ›å»ºä¸€ä¸ª`ContentTypeHandler`ç±»ï¼Œæ‰¾åˆ°`org.apache.struts2.rest.handler.ContentTypeHandler`æ¥å£

```sql
import java

class ContentTypeHandler extends RefType {
  ContentTypeHandler() {
      this.hasQualifiedName("org.apache.struts2.rest.handler", "ContentTypeHandler")
  }
}
```

2. åˆ›å»ºä¸€ä¸ª`ContentTypeHandlerToObject`ç±»ï¼Œè¯†åˆ«`org.apache.struts2.rest.handler.ContentTypeHandler`å­ç±»/å®ç°ç±»çš„`toObject`çš„æ–¹æ³•

```sql
class ContentTypeHandlerToObject extends Method {
  ContentTypeHandlerToObject() {
    this.getName() = "toObject" and
    this.getDeclaringType().getASupertype() instanceof ContentTypeHandler
  }
}
```

3. `toObject`æ–¹æ³•åº”å°†ç¬¬ä¸€ä¸ªå‚æ•°è§†ä¸ºä¸å—ä¿¡ä»»çš„ç”¨æˆ·è¾“å…¥ã€‚æŸ¥è¯¢`toObject`æ–¹æ³•çš„ç¬¬ä¸€ä¸ªå‚æ•°

```sql
from ContentTypeHandlerToObject toObject
select toObject.getParameter(0)
```

## Unsafe XML deserialization

ï¼ˆè‹±æ–‡å†™çš„å¾ˆé€šä¿—æ˜“æ‡‚ï¼Œå°±ç›´æ¥æ¬è¿äº†ï¼‰

We have now identified (a) places in the program which receive untrusted data and (b) places in the program which potentially perform unsafe XML deserialization. We now want to tie these two together to ask: does the untrusted data ever *flow* to the potentially unsafe XML deserialization call?

In program analysis we call this a *data flow* problem. Data flow helps us answer questions like: does this expression ever hold a value that originates from a particular other place in the program?

We can visualize the data flow problem as one of finding paths through a directed graph, where the nodes of the graph are elements in program, and the edges represent the flow of data between those elements. If a path exists, then the data flows between those two nodes.

Consider this example Java method:

```java
int func(int tainted) {
   int x = tainted;
   if (someCondition) {
     int y = x;
     callFoo(y);
   } else {
     return x;
   }
   return -1;
}
```

The data flow graph for this method will look something like this:

![image-20230404110635854](../.gitbook/assets/image-20230404110635854.png)

This graph represents the flow of data from the tainted parameter. The nodes of graph represent program elements that have a value, such as function parameters and expressions. The edges of this graph represent flow through these nodes.

CodeQL for Java provides data flow analysis as part of the standard library. You can import it using `semmle.code.java.dataflow.DataFlow`. The library models nodes using the `DataFlow::Node` CodeQL class. These nodes are separate and distinct from the AST (Abstract Syntax Tree, which represents the basic structure of the program) nodes, to allow for flexibility in how data flow is modeled.

```sql
/**
* @kind path-problem
*/
import java
import semmle.code.java.dataflow.DataFlow
import DataFlow::PathGraph

class ContentTypeHandler extends RefType {
  ContentTypeHandler() {
    this.hasQualifiedName("org.apache.struts2.rest.handler", "ContentTypeHandler")
  }
}

class ContentTypeHandlerToObject extends Method {
  ContentTypeHandlerToObject() {
    this.getName() = "toObject" and
    this.getDeclaringType().getASupertype() instanceof ContentTypeHandler
  }
}


predicate isXMLDeserialized(Expr arg) {
  exists(MethodAccess fromXML | 
    fromXML.getMethod().getName() = "fromXML" and
    arg = fromXML.getArgument(0)  
  )
}


class StrutsUnsafeDeserializationConfig extends DataFlow::Configuration {
  StrutsUnsafeDeserializationConfig() { this = "StrutsUnsafeDeserializationConfig" }
  override predicate isSource(DataFlow::Node source) {
    exists(ContentTypeHandlerToObject toObject |
      source.asParameter() = toObject.getParameter(0)
    )
  }
  override predicate isSink(DataFlow::Node sink) {
    exists(Expr arg |
      isXMLDeserialized(arg) and
      sink.asExpr() = arg
    )
  }
}

from StrutsUnsafeDeserializationConfig config, DataFlow::PathNode source, DataFlow::PathNode sink
where config.hasFlowPath(source, sink)
select sink, source, sink, "Unsafe XML deserialization"
```

# 0x03 CodeQL Recipe for Java

## RefType

> getACallable() è·å–æ‰€æœ‰å¯ä»¥è°ƒç”¨æ–¹æ³•(åŒ…æ‹¬æ„é€ æ–¹æ³•)
>
> getAMember() è·å–æ‰€æœ‰æˆå‘˜ï¼Œå…¶ä¸­åŒ…æ‹¬è°ƒç”¨æ–¹æ³•ï¼Œå­—æ®µå’Œå†…éƒ¨ç±»
>
> getAField() è·å–æ‰€æœ‰å­—æ®µ
>
> getAMethod() è·å–æ‰€æœ‰æ–¹æ³•
>
> getASupertype() è·å–çˆ¶ç±»
>
> getAnAncestor() è·å–æ‰€æœ‰çš„çˆ¶ç±»ç›¸å½“äºé€’å½’çš„getASupertype*()
>
> hasQualifiedName(packageName, className) æ ‡è¯†å…·æœ‰ç»™å®šåŒ…åå’Œç±»åçš„ç±»

## Method

> getName() è·å–ç±»å
>
> getDeclaringType()  è·å–æ–¹æ³•çš„å£°æ˜ç±»å‹
>
> getParameter(int index) è·å–ç´¢å¼•ä¸ºindexçš„å‚æ•°ï¼ˆç´¢å¼•ä»0å¼€å§‹ï¼‰